def versionNumber = System.getenv('versionNumber') != null ? System.getenv('versionNumber') : 'SNAPSHOT'
ext.projectIds = ['group': 'teamcity-tray-notifier-plugin', 'version': versionNumber, 'artifact': 'tray-notifier-plugin']

group = projectIds.group
version = projectIds.version

allprojects {
    group = projectIds.group
    version = projectIds.version
}

subprojects {
    apply plugin: 'java'
    sourceCompatibility = 1.6

    repositories {
        mavenCentral()
        maven {
            name 'JetBrains Maven Repository'
            url 'http://download.jetbrains.com/teamcity-repository'
        }
    }

    test.useTestNG()
    jar.version = null
}

project(':plugin-notifier-server') {
    dependencies {
        compile 'org.jetbrains.teamcity:server-api:9.0'
        compile 'com.google.code.gson:gson:2.5'
        runtime 'javax.servlet:jstl:1.1.2'
    }
    
    task distrib(type: Zip) {
        archiveName = projectIds.artifact + ".zip"
    
        from (libsDir) {
            into 'server'
        }
        
        from (configurations.compile) {
            include 'gson-*.jar'
            include 'plugin-notifier-*.jar'
            into 'server'
        }
        
        from ('teamcity-plugin.xml') {
            filter {
                it.replaceAll('@Plugin_Version@', projectIds.version)
            }
        }        
    }
    
    artifacts {
        archives distrib
    }
}